#{fixture 'test.TestDataLoader' /}
        
#{selenium '1000 - New user registration'}
  clearSession()

  open('/')
  clickAndWait('link=Register!')
  type('registration.firstName','Foo')
  type('registration.lastName','Bar')
  type('registration.email','foo@bar.com')
  type('registration.displayName','foo')
  type('registration.password1','foo')
  type('registration.password2','foo')
  clickAndWait('css=input[type=submit]')
  verifyTextPresent('Your account was successfully created! An email has been sent to foo@bar.com for activation ')

  # Verify if an error is displayed if the account is not active
  clickAndWait('link=Login')
  type('username','foo')
  type('password','foo')
  clickAndWait('signin')
  verifyTextPresent('Your account is not yet active. Active it following the link in the mail. ')

  # Verify correct login after account activation
  open('/registration/activate/testActivationToken')
  verifyTextPresent('Your account is now active! ')
  verifyTextPresent('Foo Bar')
#{/selenium}